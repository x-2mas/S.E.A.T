
#
#
# S.E.A.T | create_seat
# Creates a new S.E.A.T
#
#


/// Positioned as calling enity,
positioned('as @s'){

    /// Summon a new one (in the sky to hide summoning animations)
    /summon minecraft:fox ~ 999 ~ {Tags:[x_EID,x_SEAT_NS,x_SEAT_UNLINKED,x_SEAT,x_SEAT_BASE,x_SEAT_NO_RIDE,x_NO_COLLIDE,x_KILL_ON_UNINSTALL],CustomName:"[\"\",{\"text\":\"S.E.A.T BASE\"}]",ActiveEffects:[{Id:14,Duration:2147483647,ShowParticles:0b,Ambient:1b}],Glowing:0,DeathTime:10,NoAI:1,Health:1000000,Silent:1,Invulnerable:0,DeathLootTable:"minecraft:empty", Age:-2147483648, Sleeping:1, ForcedAge:-2147483648, InLove:0, Attributes:[{Name:"generic.movement_speed",Base:-1000},{Name:"generic.max_health",Base:1000000}], Passengers:[{id:"minecraft:llama",Tags:[x_SEAT_NS,x_SEAT_UNLINKED,x_SEAT,x_SEAT_SURFACE,x_SEAT_NO_RIDE,x_NO_COLLIDE,x_KILL_ON_UNINSTALL],CustomName:"[\"\",{\"text\":\" \"}]",Attributes:[{Name:"generic.movement_speed",Base:-1000},{Name:"generic.max_health",Base:10}],Tame:1,Silent:1,Glowing:0,DeathTime:10,NoAI:1,Health:10,Invulnerable:1,DeathLootTable:"minecraft:empty",Saddle:1,ActiveEffects:[{Id:14,Duration:2147483647,ShowParticles:0b,Ambient:1b}]}]}

    /// Teleport summoned S.E.A.T below player, positioned so that S.E.A.T surface is slightly below player's butt and facing straight
    rotated('~ 0') {
        /tp @e[tag=x_SEAT_NS,tag=x_SEAT_BASE,tag=x_SEAT_UNLINKED,tag=!x_TP_KILL,sort=nearest,limit=1] ~ ~-1.4 ~ facing ^ ^-1.6 ^2
    }

    /// As the closest un-linked S.E.A.T Surface,
    asat('@e[tag=x_SEAT_NS,tag=x_SEAT_SURFACE,tag=x_SEAT_UNLINKED,tag=!x_TP_KILL,sort=nearest,limit=1]'){

        /// Assign player EID to Surface S.E.A.T ID
        /scoreboard players operation @s x_SEAT_ID = @a[tag=x_EID,tag=x_SEAT_NS,tag=x_MAY_SIT,tag=!x_MAY_SIT_SKIP,sort=nearest,limit=1] x_EID

        /// Remove unlinked tag
        /tag @s remove x_SEAT_UNLINKED
    }

    /// As the closest un-linked S.E.A.T Base,
    asat('@e[tag=x_SEAT_NS,tag=x_SEAT_BASE,tag=x_SEAT_UNLINKED,tag=!x_TP_KILL,sort=nearest,limit=1]'){

        /// Assign player EID to Base S.E.A.T ID
        /scoreboard players operation @s x_SEAT_ID = @a[tag=x_EID,tag=x_SEAT_NS,tag=x_MAY_SIT,tag=!x_MAY_SIT_SKIP,sort=nearest,limit=1] x_EID
        
        /// Start (or extend) a auto-kill timeout on it
        /scoreboard players set @s x_SEAT_TICK 40
        
        /// Remove unlinked tag
        /tag @s remove x_SEAT_UNLINKED
    }  
}