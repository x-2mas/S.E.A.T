
#
#
# S.E.A.T | seat_physics 
# Handles S.E.A.T base physics
#
#



/// If seat base tagged as just finished falling,
if('entity @s[tag=x_SEAT_POST_FALLING]'){
        

    /// If bounce-off-floor is enabled,
    if('data storage x_seat flags{bounceOffFloor:1}'){

        /// Teleport to align player butt with ground
        /tp @s ~ ~-1.9 ~ 
    }


    /// If seat physics is enabled and seat anchor missing,
    if('data storage x_seat flags{physics:1}' && !'entity @e[tag=x_SEAT_NS,tag=x_SEAT_BASE_ANCHOR]'){
        
        /// Create a new seat anchor
        /function x_seat:create_anchor

        /// Tag seat as anchored
        /tag @s add x_SEAT_ANCHORED

        /// As all seat anchors with seat ID matching base,
        asat('@e[tag=x_SEAT_NS,tag=x_SEAT_ANCHOR]'), if('score @s x_SEAT_ID = @e[tag=x_SEAT_BASE,tag=!x_SEAT_BASE_SKIP,limit=1] x_EID'){

            /// Copy any residual fall distance in base onto the summoned anchor
            /data modify entity @s FallDistance set from entity @e[tag=x_SEAT_BASE,tag=!x_SEAT_BASE_SKIP,limit=1] FallDistance

            /// Copy any residual seat motion in base onto the summoned anchor
            /data modify entity @s Motion set from entity @e[tag=x_SEAT_BASE,tag=!x_SEAT_BASE_SKIP,limit=1] Motion
        }
    }


    /// Turn off AI (no longer directly responsive to physics)
    /data modify entity @s {} merge value {NoAI:1}


    /// Restore Y attachment
    /scoreboard players set @s x_ATTACH_Y -190
    
    /// Tag as attached to anchor
    /tag @s add x_ATTACH
    /tag @s add x_ATTACH_ROTATE
    
    /// Queue for attaching
    /scoreboard players add @s x_ATTACH__T 1

    /// Remove falling tags
    /tag @s remove x_SEAT_FALLING
    /tag @s remove x_SEAT_POST_FALLING
}



/// If seat is tagged as falling and seat has landed,
if('entity @s[tag=x_SEAT_FALLING]'){

    /// If seat has landed, or if anchor is attached to some entity
    if('entity @s[nbt={OnGround:1b}]' || 'entity @e[tag=x_SEAT_BASE_ANCHOR,tag=x_ATTACH]'){

        /// Reset fall distance once landed
        /data modify entity @s FallDistance set value 0

        /// Force-UnQueue from attaching
        /scoreboard players set @s x_ATTACH__T 0

        /// Remove attached tag (if any, temporarily skip teleporting to anchor)
        /tag @s remove x_ATTACH

        /// Tag as just finished falling
        /tag @s add x_SEAT_POST_FALLING
    }
}



/// Run actions centered at seated player's butt
positioned('^ ^1.9 ^'){

    /// If seat anchor (armor stand) is missing at player's butt position,
    if(!'entity @e[tag=x_SEAT_NS,tag=x_SEAT_BASE_ANCHOR,distance=..0.15]'){
    
        /// Tag seat as moving
        /tag @s add x_SEAT_MOVING
        

    /// If seat physics anchor is present at player's butt position
    } else {

        /// Remove seat moving tag (if any)
        /tag @s remove x_SEAT_MOVING
    }
}








