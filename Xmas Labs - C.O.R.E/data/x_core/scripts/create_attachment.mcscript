

#
#
# C.O.R.E | create_attachment
# Create attachments between entities
#
#



/// If primary attaching entity (entity being attached-to) doesn't have an EID, give it one
asat('@e[tag=x_ATTACH_NEW_PRIMARY,limit=1,sort=nearest]'), if(!'score @s x_EID matches 1..'){
    /function x_core:assign_eid
}


/// If secondary attaching entity (entity being attached) doesn't have an EID, give it one
if(!'score @s x_EID matches 1..'){
    /function x_core:assign_eid
}


/// Do as all entities tagged for attachment creation who are the secondary attachment entity (entity being attached)
asat('@e[tag=x_ATTACH_NEW_SECONDARY]'){
    
    /// Assign EID of entity below seat as parent EID of anchor
    /scoreboard players operation @s x_PID = @e[tag=x_ATTACH_NEW_PRIMARY,limit=1,sort=nearest] x_EID

    /// Set X distance between primary and secondary as X attachment
    /execute store result score @s x_R0 run data get entity @e[tag=x_ATTACH_NEW_PRIMARY,limit=1,sort=nearest] Pos[0] 1000000
    /execute store result score @s x_ATTACH_X run data get entity @s Pos[0] 1000000
    /scoreboard players operation @s x_ATTACH_X -= @s x_R0
    
    /// Set Y distance between primary and secondary as Y attachment
    /execute store result score @s x_R0 run data get entity @e[tag=x_ATTACH_NEW_PRIMARY,limit=1,sort=nearest] Pos[1] 1000000
    /execute store result score @s x_ATTACH_Y run data get entity @s Pos[1] 1000000
    /scoreboard players operation @s x_ATTACH_Y -= @s x_R0
    
    /// Set Z distance between primary and secondary as Z attachment
    /execute store result score @s x_R0 run data get entity @e[tag=x_ATTACH_NEW_PRIMARY,limit=1,sort=nearest] Pos[2] 1000000
    /execute store result score @s x_ATTACH_Z run data get entity @s Pos[2] 1000000
    /scoreboard players operation @s x_ATTACH_Z -= @s x_R0

    /// Set Y-Rotation difference between primary and secondary as Y-Rotation offset
    /execute store result score @s x_R0 run data get entity @e[tag=x_ATTACH_NEW_PRIMARY,limit=1,sort=nearest] Rotation[0] 10
    /execute store result score @s x_ATTACH_RY run data get entity @s Rotation[0] 10
    /scoreboard players operation @s x_ATTACH_RY -= @s x_R0

    /// Set X-Rotation difference between primary and secondary as X-Rotation offset
    /execute store result score @s x_R0 run data get entity @e[tag=x_ATTACH_NEW_PRIMARY,limit=1,sort=nearest] Rotation[1] 10
    /execute store result score @s x_ATTACH_RX run data get entity @s Rotation[1] 10
    /scoreboard players operation @s x_ATTACH_RX -= @s x_R0

    /// Remove create attachment tags
    /tag @e[tag=x_ATTACH_NEW_PRIMARY,limit=1,sort=nearest] remove x_ATTACH_NEW_PRIMARY
    /tag @s remove x_ATTACH_NEW_SECONDARY
}   